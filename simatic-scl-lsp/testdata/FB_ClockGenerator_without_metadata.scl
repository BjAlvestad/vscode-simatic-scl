FUNCTION_BLOCK "fb_ClockGenerator_V15.0.2"
   VAR_INPUT 
      ir_PulsePauseLengthSeconds : Real;   // Puls Pause LÃ¤nge in Sekunden
      ir_PulsePauseRatioPercent : Real := 50.0;   // Pulse Pause Ratio in Prozent
      ib_Start : Bool;
   END_VAR

   VAR_OUTPUT 
      ob_Clock : Bool;
   END_VAR

   VAR 
      instTofTimePulse : TOF_TIME;   // -
      instTofTimePause : TOF_TIME;   // -
      statTimePulse : Time;   // -
      statTimePause : Time;   // -
   END_VAR

   VAR_TEMP 
      tempPulseRate : Real;   // -
   END_VAR


BEGIN
	#tempPulseRate := #ir_PulsePauseRatioPercent / 100;
	#statTimePulse := UDINT_TO_TIME(REAL_TO_UDINT(1000.0 * #ir_PulsePauseLengthSeconds * #tempPulseRate));    
	#statTimePause := UDINT_TO_TIME(REAL_TO_UDINT(1000.0 * #ir_PulsePauseLengthSeconds * (1-#tempPulseRate)));
	#instTofTimePulse(IN := NOT #instTofTimePause.Q,PT := #statTimePulse);
	#instTofTimePause(IN := #instTofTimePulse.Q, PT := #statTimePause);
	IF #ib_Start THEN #ob_Clock := #instTofTimePulse.Q;
	ELSE #ob_Clock := FALSE; END_IF;
END_FUNCTION_BLOCK